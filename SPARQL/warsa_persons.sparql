PREFIX wsch: <http://ldf.fi/schema/warsa/>
PREFIX wsa: <http://ldf.fi/schema/warsa/actors/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX text: <http://jena.apache.org/text#>
SELECT ?person ?given ?family ?birth_begin ?birth_end ?birth_place ?death_begin ?death_end ?death_place ?rank (SAMPLE(?rank_lvl) AS ?rank_level) (MAX(?event_begin) as ?activitity_end)
WHERE {
      ?person a wsch:Person .
      ?person foaf:firstName ?given .
      ?person foaf:familyName ?family .
  OPTIONAL {
    ?person ^crm:P98_brought_into_life ?birth .
    ?birth crm:P4_has_time-span [
        crm:P82a_begin_of_the_begin ?birth_begin ;
        crm:P82b_end_of_the_end ?birth_end ;
        ]
    OPTIONAL { ?birth crm:P7_took_place_at ?birth_place . }
  }
  OPTIONAL {
    ?person ^crm:P100_was_death_of ?death .
    ?death crm:P4_has_time-span [
        crm:P82a_begin_of_the_begin ?death_begin ;
        crm:P82b_end_of_the_end ?death_end ;
        ]
    OPTIONAL { ?death crm:P7_took_place_at ?death_place . }
  }
  OPTIONAL {
    ?promo crm:P11_had_participant ?person .
    ?promo a wsch:Promotion .
    ?promo wsa:hasRank ?rank .
    ?rank wsa:level ?rank_lvl .
    ?person ^crm:P11_had_participant/wsa:hasRank/wsa:level ?rank_level2 .
  }
  OPTIONAL { ?person ^crm:P11_had_participant/crm:P4_has_time-span/crm:P82a_begin_of_the_begin ?event_begin }
}
GROUP BY ?person ?given ?family ?birth_begin ?birth_end ?birth_place ?death_begin ?death_end ?death_place ?rank
HAVING(MAX(COALESCE(?rank_lvl, 1)) >= MAX(COALESCE(?rank_level2, 1)))
